plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs'
}

def APP_VERSION = "2.4.103"

android {
    compileSdkVersion 33
    buildToolsVersion '33.0.0'

    defaultConfig {
        applicationId "com.sygic.driving.testapp"
        targetSdkVersion 33
        minSdkVersion 21

        versionCode 103
        versionName APP_VERSION

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

        manifestPlaceholders.googleMapsKey = gradle.getGoogleMapsKey()
    }

    defaultConfig {
        buildConfigField "String", "APP_VERSION", "\"${APP_VERSION}\""
        buildConfigField "String", "SYGIC_LICENSE", "\"${gradle.getSygicLicense()}\""
    }

    buildTypes {
        debug {
            multiDexEnabled true
            minifyEnabled false
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    def dimMobileServices = "services"
    flavorDimensions(dimMobileServices)
    productFlavors {
        gms {
            dimension dimMobileServices
        }
        hms {
            dimension dimMobileServices
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding true
    }

    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += "-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"
    }
}

repositories {
    maven { url 'https://maven.testfairy.com' }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.google.android.gms:play-services-maps:18.1.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    if (gradle.getDrivingLibPath().isEmpty()) {
        gmsImplementation("com.sygic.adas:driving:$driving_version")
        hmsImplementation("com.sygic.adas:driving-hms:$driving_version")
    } else {
        implementation project(':driving-lib')
    }

    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.gms:play-services-location:21.0.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'com.google.guava:guava:27.1-android'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation "androidx.security:security-crypto:1.1.0-alpha03"

    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.0'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.4.0"

    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    implementation "androidx.fragment:fragment-ktx:$fragment_version"

    implementation 'com.squareup.retrofit2:retrofit:2.6.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.2'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.0'

    implementation "androidx.datastore:datastore-preferences:$datastore_version"

    implementation "com.sygic.lib.auth:sygicauth-android:1.0.2"

    //noinspection GradleDynamicVersion
    implementation 'com.testfairy:testfairy-android-sdk:1.+@aar'

    def hiltVersion = "2.44.2"
    implementation "com.google.dagger:hilt-android:$hiltVersion"
    kapt "com.google.dagger:hilt-compiler:$hiltVersion"
}
